// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.1.2-4'
        build_tools_version = '25.0.3'
        compile_sdk_version = 25
        min_sdk_version = 15
        target_sdk_version = 25

        libraries = [
                supportVersion: "25.3.1",
                ankoVersion   : "0.9.1a",
        ]

        GROUP = "io.vithor.frameworks.yaf"
        VERSION_NAME = "1.0.0-rc1"
    }

    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.14-eap-5"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.4'

    }
}

allprojects {

    configurations {
        javadocDeps
    }

    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    // make sure this comes *after* you apply the Android plugin
    apply plugin: 'com.getkeepsafe.dexcount'
    apply plugin: 'org.jetbrains.dokka-android'

    apply plugin: 'maven-publish'

    android {
        compileSdkVersion compile_sdk_version
        buildToolsVersion "$build_tools_version"

        defaultConfig {
            minSdkVersion min_sdk_version
            targetSdkVersion target_sdk_version
            versionCode 1
            versionName "1.0"
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile "com.winterbe:expekt:0.5.0"
        testCompile 'com.nhaarman:mockito-kotlin:1.4.0'

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}